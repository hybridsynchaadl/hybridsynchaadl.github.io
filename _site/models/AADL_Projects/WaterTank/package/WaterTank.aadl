package WaterTank
public
	with Base_Types;
	with Data_Model;
	
	system WaterTank
		features
			on_control: out event port;
			off_control: out event port;
			curr_water: in data port Base_Types::Float;
			incoming_water: in data port Base_Types::Float;
			increased_water: out data port Base_Types::Float {Data_Model::Initial_Value => ("0");};
			released_water: out data port Base_Types::Float {Data_Model::Initial_Value => ("0");};
			power: out data port Base_Types::Float {Data_Model::Initial_Value => ("0");};
			decreased_water: in data port Base_Types::Float;	
	end WaterTank;
	
	system implementation WaterTank.simple
		subcomponents
			ctrlProc: process WaterTankProc.simple;
		connections
			C1: port ctrlProc.on_control -> on_control;
			C2: port ctrlProc.off_control -> off_control;
			C3: port curr_water -> ctrlProc.curr_water;
			C4: port incoming_water -> ctrlProc.incoming_water;
			C5: port ctrlProc.increased_water -> increased_water;
			C6: port ctrlProc.released_water -> released_water;
			C7: port ctrlProc.power -> power;
			C8: port decreased_water -> ctrlProc.decreased_water;
	end WaterTank.simple;
	
	system implementation WaterTank.complex extends WaterTank.simple
		subcomponents
			ctrlProc: refined to process WaterTankProc.complex;
	end WaterTank.complex;
	
	process WaterTankProc
		features
			on_control: out event port;
			off_control: out event port;
			curr_water: in data port Base_Types::Float;
			incoming_water: in data port Base_Types::Float;
			increased_water: out data port Base_Types::Float;
			released_water: out data port Base_Types::Float;
			decreased_water: in data port Base_Types::Float;
			power: out data port Base_Types::Float;	
	end WaterTankProc;
	
	process implementation WaterTankProc.simple
		subcomponents
			ctrlThread: thread WaterTankThread.simple;
		connections
			C1: port ctrlThread.on_control -> on_control;
			C2: port ctrlThread.off_control -> off_control;
			C3: port curr_water -> ctrlThread.curr_water;
			C4: port incoming_water -> ctrlThread.incoming_water;
			C5: port ctrlThread.increased_water -> increased_water;
			C6: port ctrlThread.released_water -> released_water;
			C7: port ctrlThread.power -> power;
			C8: port decreased_water -> ctrlThread.decreased_water;
	end WaterTankProc.simple;
	
	process implementation WaterTankProc.complex extends WaterTankProc.simple
		subcomponents
			ctrlThread: refined to thread WaterTankThread.complex;		
	end WaterTankProc.complex;
	
	thread WaterTankThread
		features
			on_control: out event port;
			off_control: out event port;
			curr_water: in data port Base_Types::Float;
			incoming_water: in data port Base_Types::Float;
			increased_water: out data port Base_Types::Float;
			released_water: out data port Base_Types::Float;
			power: out data port Base_Types::Float;	
			decreased_water: in data port Base_Types::Float;
		properties
			Dispatch_Protocol => Periodic;
	end WaterTankThread;
	
	thread implementation WaterTankThread.simple
		annex behavior_specification{**
			states
				init : initial complete state;
				exec : state;
			transitions
				init -[ on dispatch ]-> exec {
					increased_water := incoming_water;
					released_water := decreased_water	
				};
				exec -[ true ]-> init {
					if (curr_water <= 33)
						power := 0.5;
						on_control!
					elsif (curr_water <= 39)
						power := 0.3;
						on_control!
					else {
						power := 0.0;
						off_control!
					}
					end if
				};
		**};
	end WaterTankThread.simple;
	
	thread implementation WaterTankThread.complex extends WaterTankThread.simple
        annex behavior_specification{**
            states
                init : initial complete state;
                exec : state;
            transitions
                init -[ on dispatch ]-> exec {
                    increased_water := incoming_water;
                    released_water := decreased_water
                };
                exec -[curr_water <= 42]-> init {
                    if (curr_water <= 30)
                        power := 0.5
                    elsif (curr_water <= 33)
                        power := 0.4
                    elsif (curr_water <= 36)
                        power := 0.3
                    elsif (curr_water <= 39)
                        power := 0.2
                    else {
                        power := 0.1
                    }
                    end if;
                    on_control!
                };
                exec -[otherwise]-> init {
                    off_control!
                };
        **};
	end WaterTankThread.complex;
	
end WaterTank;
