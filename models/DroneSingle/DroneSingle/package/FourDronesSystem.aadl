package FourDronesSystem
public
	with Drone;
	with RefDrone;
	with Data_Model;
	with Hybrid_SynchAADL;
	
	system FourDronesSystem
		properties
			Hybrid_SynchAADL::Synchronous => true;
			Hybrid_SynchAADL::Max_Clock_Deviation => 5ms;
			Period => 100ms;
			Classifier_Substitution_Rule => Type_Extension;
	end FourDronesSystem;
	
	system implementation FourDronesSystem.revu1D
		subcomponents
			drone1: system Drone::RevuDrone.simple;
			drone2: system Drone::RevuDrone.simple;
			drone3: system Drone::RevuDrone.simple;
			drone4: system Drone::RevuDrone.simple;
		connections
			d12x: port drone1.outX -> drone2.inX;
			d23x: port drone2.outX -> drone3.inX;
			d34x: port drone3.outX -> drone4.inX;
			d41x: port drone4.outX -> drone1.inX;
		properties
    		Data_Model::Initial_Value => ("param") applies to
    			drone1.environment.x, drone2.environment.x, 
    			drone3.environment.x, drone4.environment.x; 
    		Data_Model::Initial_Value => ("0") applies to
    			drone1.environment.dotx, drone2.environment.dotx, 
    			drone3.environment.dotx, drone4.environment.dotx;			
	
			Timing => Delayed applies to 
				d12x,  d23x,  d34x, d41x;
			
			Hybrid_SynchAADL::Sampling_Time => 20ms .. 30ms applies to 
				drone1.drone, drone2.drone, drone3.drone, drone4.drone;
				
			Hybrid_SynchAADL::Response_Time => 60ms .. 80ms applies to 
				drone1.drone, drone2.drone, drone3.drone, drone4.drone;
	end FourDronesSystem.revu1D;
	
	
	system implementation FourDronesSystem.form1D extends FourDronesSystem.revu1D
		subcomponents
			drone1: refined to system Drone::FormDrone.simple;
			drone2: refined to system Drone::FormDrone.simple;
			drone3: refined to system Drone::FormDrone.simple;
			drone4: refined to system Drone::FormDrone.simple;
			refDrone: system RefDrone::RefDroneControl.formation;
		connections
			r1x: port refDrone.outX -> drone1.refX;
			r2x: port refDrone.outX -> drone2.refX;
			r3x: port refDrone.outX -> drone3.refX;
			r4x: port refDrone.outX -> drone4.refX;
		properties
			Data_Model::Initial_Value => ("-0.5") applies to
    			drone1.drone.droneProc.droneThread.offsetX,
    			drone3.drone.droneProc.droneThread.offsetX;   			
    		Data_Model::Initial_Value => ("0") applies to
    			drone2.drone.droneProc.droneThread.offsetX,  			
    			drone4.drone.droneProc.droneThread.offsetX;
    			
    		Data_Model::Initial_Value => ("param") applies to
    			refDrone.env.x;		
    		Data_Model::Initial_Value => ("0") applies to
    			refDrone.env.dotx;
    			    			
			Timing => Delayed applies to 
				r1x,  r2x,  r3x, r4x;	
				
			Hybrid_SynchAADL::Sampling_Time => 20ms .. 30ms applies to refDrone.refDrone;
				
			Hybrid_SynchAADL::Response_Time => 60ms .. 80ms applies to refDrone.refDrone;	
    			
	end FourDronesSystem.form1D;
	
	
end FourDronesSystem;
