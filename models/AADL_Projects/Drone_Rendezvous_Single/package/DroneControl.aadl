package DroneControl
public
	with Base_Types;
	with DroneSpec;
        
	system Drone1DControl
		features
			inX: in data port Base_Types::Float;
			outX : out data port Base_Types::Float;
			currX: in data port Base_Types::Float;
			velX : out data port Base_Types::Float;
	end Drone1DControl;
	system implementation Drone1DControl.impl
		subcomponents
			droneProc: process Drone1DControlProc.impl;
		connections
			C1: port currX -> droneProc.currX;
			C2: port inX -> droneProc.inX;
			C3: port droneProc.velX -> velX;
			C4: port droneProc.outX -> outX;
	end Drone1DControl.impl;
	
	system Drone2DControl extends Drone1DControl
		features
			inY: in data port Base_Types::Float;
			outY : out data port Base_Types::Float;
			currY: in data port Base_Types::Float;
			velY : out data port Base_Types::Float;
	properties
			Classifier_Substitution_Rule => Type_Extension;			
	end Drone2DControl;
	system implementation Drone2DControl.impl extends Drone1DControl.impl
		subcomponents
			droneProc: refined to process Drone2DControlProc.impl;
		connections
			C5: port currY -> droneProc.currY;
			C6: port inY -> droneProc.inY;
			C7: port droneProc.velY -> velY;
			C8: port droneProc.outY -> outY;
	end Drone2DControl.impl;	
	
	process Drone1DControlProc
		features
			inX: in data port Base_Types::Float;
			outX : out data port Base_Types::Float;
			currX: in data port Base_Types::Float;
			velX : out data port Base_Types::Float;
	end Drone1DControlProc;
	process implementation Drone1DControlProc.impl
		subcomponents
			droneThread: thread Drone1DControlThread.impl;
		connections
			C1: port currX -> droneThread.currX;
			C2: port inX -> droneThread.inX;
			C3: port droneThread.velX -> velX;
			C4: port droneThread.outX -> outX;
	end Drone1DControlProc.impl;

	process Drone2DControlProc extends Drone1DControlProc
		features
			inY: in data port Base_Types::Float;
			outY : out data port Base_Types::Float;
			currY: in data port Base_Types::Float;
			velY : out data port Base_Types::Float;
		properties
			Classifier_Substitution_Rule => Type_Extension;					
	end Drone2DControlProc;
	process implementation Drone2DControlProc.impl extends Drone1DControlProc.impl
		subcomponents
			droneThread: refined to thread Drone2DControlThread.impl;
		connections
			C5: port currY -> droneThread.currY;
			C6: port inY -> droneThread.inY;
			C7: port droneThread.velY -> velY;
			C8: port droneThread.outY -> outY;
	end Drone2DControlProc.impl;	
	
	thread Drone1DControlThread
		features
			inX: in data port Base_Types::Float;
			outX : out data port Base_Types::Float;
			currX: in data port Base_Types::Float;
			velX : out data port Base_Types::Float;
		properties
			Dispatch_Protocol => Periodic;
	end Drone1DControlThread;
	thread implementation Drone1DControlThread.impl
		annex behavior_specification {**
			variables
				nx : Base_Types::Float;
			states
				init : initial complete state;
				exec, output : state;
			transitions
				init -[on dispatch]-> exec;
				exec -[abs(currX - inX) < 0.1]-> output {
					velX := 0
				};
				exec -[otherwise]-> output {
					nx := - #DroneSpec::A * (currX - inX); 
					if (nx > 1)			velX := 2
					elsif (nx > -0.1)	velX := 0
					else				velX := -2
					end if
				};
				output -[ ]-> init {
					outX := currX
				};
	**};
	end Drone1DControlThread.impl;
	
	thread Drone2DControlThread extends Drone1DControlThread
		features
			inY: in data port Base_Types::Float;
			outY : out data port Base_Types::Float;
			currY: in data port Base_Types::Float;
			velY : out data port Base_Types::Float;
		properties
			Dispatch_Protocol => Periodic;
	end Drone2DControlThread;
	thread implementation Drone2DControlThread.impl extends Drone1DControlThread.impl
		annex behavior_specification {**
			variables
				nx : Base_Types::Float;
				ny : Base_Types::Float;
			states
				init : initial complete state;
				exec, output : state;
			transitions
				init -[on dispatch]-> exec;
				exec -[abs(currX - inX) < 0.1 and abs(currY - inY) < 0.1]-> output {
					velX := 0; velY := 0
				};
				exec -[otherwise]-> output {
					nx := - #DroneSpec::A * (currX - inX); 
					ny := - #DroneSpec::A * (currY - inY); 
					if (nx > 1)			velX := 2
					elsif (nx > -0.1)	velX := 0
					else				velX := -2
					end if;
					if (ny > 1)			velY := 2
					elsif (ny > -0.1)	velY := 0
					else				velY := -2
					end if
				};
				output -[ ]-> init {
					outX := currX;
					outY := currY
				};
	**};
	end Drone2DControlThread.impl;	
end DroneControl;
